buildscript {
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
plugins {
    // Add the shadowjar plugin for fat jars, kotlin plugin for kotlin
    // and the maven-publish plugin to publish shadowjar to jitpack/maven
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle.forge'

version = 'git rev-parse HEAD'.execute().text.substring(0, 10) // git hash  
group = "org.valkyrienskies.addon"
archivesBaseName = "vs-control"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_39"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    maven {
        name = "DaPorkchop_"
        url = "https://maven.daporkchop.net/"
    }
    maven {
        name = "Dogforce Games"
        url = "http://dogforce-games.com/maven/"
    }
    maven { url 'https://jitpack.io' }
    mavenCentral()
}

// Create shade configuration to indicate to shadow
// which dependencies should be shaded
configurations {
    shade
    compile.extendsFrom shade
}

dependencies {
    // The mod depends on Valkyrien Skies
    // I'm being lazy so I'll just use jitpack for now
    deobfCompile("org.valkyrienskies:Valkyrien-Skies:43248b11cb2926ae7d21124d1f9a9b6385c9796d:all") {
        transitive = false
    }

    deobfCompile("org.valkyrienskies:Valkyrien-Skies-World:d093c553ef0527052c28c9e36654a6c20831d1b4") {
        transitive = false
    }

    // GraphLib is used by node tile entities to create a graph of nodes
    shade "gigaherz.graph:GraphLib:1.4.0"

    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.72"

    // lombok
    compileOnly "org.projectlombok:lombok:1.18.10"
    annotationProcessor "org.projectlombok:lombok:1.18.10"
}

jar {
    classifier = "unshaded"
    manifest.attributes(
            "FMLAT": "vs_control_at.cfg"
    )
}

shadowJar {
    classifier = null // No -all classifier
    // All 'shade' dependencies should be put in the fat jar
    configurations = [project.configurations.shade]
    // Relocate all kotlin related libraries so as not to interfere with
    // other mods that might use kotlin
    relocate 'kotlin', 'org.valkyrienskies.addons.control.deps.kotlin'
    relocate 'jetbrains', 'org.valkyrienskies.addons.control.deps.jetbrains'
    relocate 'org.jetbrains', 'org.valkyrienskies.addons.control.deps.org.jetbrains'
    relocate 'intellij', 'org.valkyrienskies.addons.control.deps.intellij'
    relocate 'org.intellij', 'org.valkyrienskies.addons.control.deps.org.intellij'
    mergeServiceFiles() // https://github.com/johnrengelman/shadow/issues/304
}

reobf {
    jar {
        mappingType = "SEARGE"
        classpath = sourceSets.main.compileClasspath
    }
    shadowJar{
        mappingType = "SEARGE"
        classpath = sourceSets.main.compileClasspath
    }
}

tasks.jar.finalizedBy(reobfJar)
tasks.shadowJar.finalizedBy(reobfShadowJar)

publishing {
    repositories {
        def ghpUser = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
        def ghpPassword = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        if (ghpUser != null && ghpPassword != null) {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/ValkyrienSkies/Valkyrien-Skies-Control")
                credentials {
                    username = ghpUser
                    password = ghpPassword
                }
            }
        }
        if (project.hasProperty("daporkchopMavenUsername") && project.hasProperty("daporkchopMavenPassword")) {
            maven {
                credentials {
                    username = "$daporkchopMavenUsername"
                    password = "$daporkchopMavenPassword"
                }

                name = "DaPorkchop_"
                url = "https://maven.daporkchop.net/release/"
            }
        }
    }
    publications {
        maven(MavenPublication) {
            groupId = "org.valkyrienskies.addon"
            artifactId = "vs-control"
            version = project.version

            artifact jar {
                classifier null
            }
            artifact shadowJar {
                classifier "all"
            }
            artifact sourceJar {
                classifier "sources"
            }
        }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
